% TODO translation
% TODO proof-reading
\section{Returning a value}

Probably the simplest Java function is one which returns some value.
Oh, and we must keep in mind that there are no ``free'' functions in Java in common sense,
they are ``methods''. 
Each method is related to some class, so it's not possible to define
method outside of a class.
But I'll also call them ``functions'' anyway, because I used to.

\begin{lstlisting}
public class ret
{
	public static int main(String[] args) 
	{
		return 0;
	}
}
\end{lstlisting}

I'll compile it:

\begin{lstlisting}
javac ret.java
\end{lstlisting}

\dots and decompile it using standard Java utility:

\begin{lstlisting}
javap -c -verbose ret.class
\end{lstlisting}

And what I've got:

\begin{lstlisting}[caption=JDK 1.7 (excerpt)]
  public static int main(java.lang.String[]);
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=1, locals=1, args_size=1
         0: iconst_0      
         1: ireturn       
\end{lstlisting}

Java developers decide that 0 is one of the busiest constants in programming, 
so there are separate short one-byte \TT{iconst\_0} instruction which pushes 0
\footnote{Just like in MIPS, where a separate register for zero constant exists: 
\myref{MIPS_zero_register}.}.
There are also \TT{iconst\_1} (which pushes 1), \TT{iconst\_2}, etc, 
up to \TT{iconst\_5}. 
There are also \TT{iconst\_m1} which pushes -1.

Stack is used in JVM for data passing into functions to be called and also returning values.
So \TT{iconst\_0} pushed 0 into stack.
\TT{ireturn} returns integer value (\IT{i} in name mean \IT{integer}) from the \ac{TOS}.

Let's rewrite our example slightly:

\begin{lstlisting}
public class ret
{
	public static int main(String[] args)
	{
		return 1234;
	}
}
\end{lstlisting}

\dots we got:

\begin{lstlisting}[caption=JDK 1.7 (excerpt)]
  public static int main(java.lang.String[]);
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=1, locals=1, args_size=1
         0: sipush        1234
         3: ireturn       
\end{lstlisting}

\TT{sipush} (short integer) pushes 1234 value into stack.
\IT{short} in name means a presence of 16-bit value encoded in instruction. 
1234 number is indeed well fit in 16-bit value.

What about larger values?

\begin{lstlisting}
public class ret
{
	public static int main(String[] args) 
	{
		return 12345678;
	}
}
\end{lstlisting}

\begin{lstlisting}[caption=Constant pool]
...
   #2 = Integer            12345678
...
\end{lstlisting}

\begin{lstlisting}
  public static int main(java.lang.String[]);
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=1, locals=1, args_size=1
         0: ldc           #2                  // int 12345678
         2: ireturn       
\end{lstlisting}

It's not possible to encode 32-bit number in JVM instruction's opcodes.
So 32-bit number 12345678 is stored in so called ``constant pool'' which is, let's say,
library of most used constants (including strings, objects, etc).

This way of passing constants is not unique to JVM. 
MIPS, ARM and other RISC CPUs also can't encode 32-bit numbers
in 32-bit opcode, so RISC CPU code (including MIPS and ARM) should construct values in steps, 
or to keep them in data segment.

MIPS code is also traditionally has constant pool, named ``literal pool'', these are segments
called ``.lit4'' (for 32-bit single precision float point number constants storage) and ``.lit8''
(for 64-bit double precision float point number constants storage).

Let's also try other data types!

Boolean:

\begin{lstlisting}
public class ret
{
	public static boolean main(String[] args) 
	{
		return true;
	}
}
\end{lstlisting}

\begin{lstlisting}
  public static boolean main(java.lang.String[]);
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=1, locals=1, args_size=1
         0: iconst_1      
         1: ireturn       
\end{lstlisting}

This code is no different from one returning integer 1.
32-bit data slots in stack are also used for boolean values, like in C++.
But one probably could not use returned boolean value as integer or vice versa --- type information
is recoreded in class file and checked.

The same story about 16-bit short:

\begin{lstlisting}
public class ret
{
	public static short main(String[] args) 
	{
		return 1234;
	}
}
\end{lstlisting}

\begin{lstlisting}
  public static short main(java.lang.String[]);
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=1, locals=1, args_size=1
         0: sipush        1234
         3: ireturn       
\end{lstlisting}

\dots and char!

\begin{lstlisting}
public class ret
{
	public static char main(String[] args) 
	{
		return 'A';
	}
}
\end{lstlisting}

\begin{lstlisting}
  public static char main(java.lang.String[]);
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=1, locals=1, args_size=1
         0: bipush        65
         2: ireturn       
\end{lstlisting}

\TT{bipush} mean ``push byte'', but \IT{char} in Java is 16-bit UTF-16 character, 
so it's equivalent to \IT{short}.

Let's also try byte:

\begin{lstlisting}
public class retc
{
	public static byte main(String[] args) 
	{
		return 123;
	}
}
\end{lstlisting}

\begin{lstlisting}
  public static byte main(java.lang.String[]);
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=1, locals=1, args_size=1
         0: bipush        123
         2: ireturn       
\end{lstlisting}

One may ask, why to bother with 16-bit \IT{short} date type which is internally works
as 32-bit integer?
Why use \IT{char} data type if it is the same as \IT{short} data type?

The answer is simple: for data type control and source code readability.
\IT{char} may essentially be the same as \IT{short} but we quickly grasp that it's placeholder for
UTF-16 character, not for some other integer.
When using \IT{short} we may show to everyone that a variable range is limited by 16 bits.

It's a very good idea to use \IT{boolean} where it needs to, instead of C-style \IT{int} for the same
purpose.

There are also 64-bit data type in Java:

\begin{lstlisting}
public class ret3
{
	public static long main(String[] args)
	{
		return 1234567890123456789L;
	}
}
\end{lstlisting}

\begin{lstlisting}[caption=Constant pool]
...
   #2 = Long               1234567890123456789l
...
\end{lstlisting}

\begin{lstlisting}
  public static long main(java.lang.String[]);
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=2, locals=1, args_size=1
         0: ldc2_w        #2                  // long 1234567890123456789l
         3: lreturn       
\end{lstlisting}

The 64-bit number is also stored in constant pool, \TT{ldc2\_w} loads it and \TT{lreturn} 
(long return) returns it.

\TT{ldc2\_w} instruction is also used to load double precision floating point numbers 
(which also occupies 64 bits) from constant pool:

\begin{lstlisting}
public class ret
{
	public static double main(String[] args)
	{
		return 123.456d;
	}
}
\end{lstlisting}

\begin{lstlisting}[caption=Constant pool]
...
   #2 = Double             123.456d
...
\end{lstlisting}

\begin{lstlisting}
  public static double main(java.lang.String[]);
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=2, locals=1, args_size=1
         0: ldc2_w        #2                  // double 123.456d
         3: dreturn       
\end{lstlisting}

Meaning of \TT{dreturn} is ``return double''.

And finally, single precision floating point number:

\begin{lstlisting}
public class ret
{
	public static float main(String[] args)
	{
		return 123.456f;
	}
}
\end{lstlisting}

\begin{lstlisting}[caption=Constant pool]
...
   #2 = Float              123.456f
...
\end{lstlisting}

\begin{lstlisting}
  public static float main(java.lang.String[]);
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=1, locals=1, args_size=1
         0: ldc           #2                  // float 123.456f
         2: freturn       
\end{lstlisting}

\TT{ldc} instruction used here is the same as used for loading 32-bit number from constant pool.
Meaning of \TT{freturn} is ``return float''.

Now what about function returning nothing?

\begin{lstlisting}
public class ret
{
	public static void main(String[] args) 
	{
		return;
	}
}
\end{lstlisting}

\begin{lstlisting}
  public static void main(java.lang.String[]);
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=0, locals=1, args_size=1
         0: return        
\end{lstlisting}

This implies, \TT{return} instruction is used to return nothing.
Knowing all this, it's very easy to deduct function (or method) returning type 
from the last instruction.
