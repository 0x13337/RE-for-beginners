% TODO proof-reading
\section{\EN{Bitfields}\RU{Битовые поля}}

\EN{All bit-wise operations work just like in any other \ac{ISA}:}
\RU{Все побитовые операции работают также, как и в любой другой \ac{ISA}:}

\begin{lstlisting}
	public static int set (int a, int b) 
	{
		return a | 1<<b;
	}

	public static int clear (int a, int b) 
	{
		return a & (~(1<<b));
	}
\end{lstlisting}

\begin{lstlisting}
  public static int set(int, int);
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=3, locals=2, args_size=2
         0: iload_0       
         1: iconst_1      
         2: iload_1       
         3: ishl          
         4: ior           
         5: ireturn       

  public static int clear(int, int);
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=3, locals=2, args_size=2
         0: iload_0       
         1: iconst_1      
         2: iload_1       
         3: ishl          
         4: iconst_m1     
         5: ixor          
         6: iand          
         7: ireturn       
\end{lstlisting}

\EN{\TT{iconst\_m1} loads $-1$ in the stack, it's the same as the \TT{0xFFFFFFFF} number.}
\RU{\TT{iconst\_m1} загружает $-1$ в стек, это то же что и значение \TT{0xFFFFFFFF}.}
\EN{XORing with \TT{0xFFFFFFFF} has the same effect of inverting all bits}
\RU{Операция XOR с \TT{0xFFFFFFFF} в одном из операндов, это тот же эффект что инвертирование всех бит} (\myref{XOR_property}).

\EN{Let's extend all data types to 64-bit \IT{long}:}
\RU{Попробуем также расширить все типы данных до 64-битного \IT{long}:}

\begin{lstlisting}
	public static long lset (long a, int b) 
	{
		return a | 1<<b;
	}

	public static long lclear (long a, int b) 
	{
		return a & (~(1<<b));
	}
\end{lstlisting}

\begin{lstlisting}
  public static long lset(long, int);
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=4, locals=3, args_size=2
         0: lload_0       
         1: iconst_1      
         2: iload_2       
         3: ishl          
         4: i2l           
         5: lor           
         6: lreturn       

  public static long lclear(long, int);
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=4, locals=3, args_size=2
         0: lload_0       
         1: iconst_1      
         2: iload_2       
         3: ishl          
         4: iconst_m1     
         5: ixor          
         6: i2l           
         7: land          
         8: lreturn       
\end{lstlisting}

\EN{The code is the same, but instructions with \IT{l} prefix are used, which operate 
on 64-bit values.}
\RU{Код такой же, но используются инструкции с префиксом \IT{l}, которые работают 
с 64-битными значениями.}
\EN{Also, the second argument of the function still is of type \IT{int}, and when the 32-bit value in it 
needs to be promoted to 64-bit value the \TT{i2l} instruction is used, 
which essentially extend the value of an \IT{integer} type to a \IT{long} one.}
\RU{Так же, второй аргумент функции все еще имеет тип \IT{int}, и когда 32-битное число в нем
должно быть расширено до 64-битного значения, используется инструкция \TT{i2l}, 
которая расширяет значение типа \IT{integer} в значение типа \IT{long}.}
