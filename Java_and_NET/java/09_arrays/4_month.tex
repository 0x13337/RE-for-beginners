% TODO translation
% TODO proof-reading
\subsection{Pre-initialized array of strings}
\label{Java_2D_array_month}

\begin{lstlisting}
class Month
{
	public static String[] months = 
	{
		"January", 
		"February", 
		"March", 
		"April",
		"May",
		"June",
		"July",
		"August",
		"September",
		"October",
		"November",
		"December"
	};

	public String get_month (int i)
	{
		return months[i];
	};
} 
\end{lstlisting}

\TT{get\_month()} function is simple:

\begin{lstlisting}
  public java.lang.String get_month(int);
    flags: ACC_PUBLIC
    Code:
      stack=2, locals=2, args_size=2
         0: getstatic     #2                  // Field months:[Ljava/lang/String;
         3: iload_1       
         4: aaload        
         5: areturn       
\end{lstlisting}

\TT{aaload} operates on array of references.
Java String is object, so \IT{a}-instructions are used to operate on them.
\TT{areturn} returns reference to String object.

How \TT{months[]} array is initialized?

\begin{lstlisting}
  static {};
    flags: ACC_STATIC
    Code:
      stack=4, locals=0, args_size=0
         0: bipush        12
         2: anewarray     #3                  // class java/lang/String
         5: dup           
         6: iconst_0      
         7: ldc           #4                  // String January
         9: aastore       
        10: dup           
        11: iconst_1      
        12: ldc           #5                  // String February
        14: aastore       
        15: dup           
        16: iconst_2      
        17: ldc           #6                  // String March
        19: aastore       
        20: dup           
        21: iconst_3      
        22: ldc           #7                  // String April
        24: aastore       
        25: dup           
        26: iconst_4      
        27: ldc           #8                  // String May
        29: aastore       
        30: dup           
        31: iconst_5      
        32: ldc           #9                  // String June
        34: aastore       
        35: dup           
        36: bipush        6
        38: ldc           #10                 // String July
        40: aastore       
        41: dup           
        42: bipush        7
        44: ldc           #11                 // String August
        46: aastore       
        47: dup           
        48: bipush        8
        50: ldc           #12                 // String September
        52: aastore       
        53: dup           
        54: bipush        9
        56: ldc           #13                 // String October
        58: aastore       
        59: dup           
        60: bipush        10
        62: ldc           #14                 // String November
        64: aastore       
        65: dup           
        66: bipush        11
        68: ldc           #15                 // String December
        70: aastore       
        71: putstatic     #2                  // Field months:[Ljava/lang/String;
        74: return        
\end{lstlisting}

\TT{anewarray} creates new array of references (hence \IT{a} in prefix).
Object type is defined in \TT{anewarray} operand, it is ``java/lang/String'' here.
\TT{bipush 12} before \TT{anewarray} sets array size.
We see here new instruction for us: \TT{dup}.
It's very well-known stack computers (including Forth programming language) instruction
which just duplicates value at \ac{TOS}.
It is used here to duplicate reference to array, because \TT{aastore} instruction pops
reference to array from stack, but subsequent \TT{aastore} will need it again.
Java compiler concluded that it's better to generate \TT{dup} instead of generating 
\TT{getstatic} instruction before each array store operation (i.e., 11 times).

\TT{aastore} puts reference (to string) into array at index which is taken from the operand stack.

Finally, \TT{putstatic} puts reference to the newly created array into the second field 
of our object, i.e., \IT{months} field.
