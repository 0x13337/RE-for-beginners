% TODO translation
% TODO proof-reading
\section{Exceptions}

Let's rework our \IT{Month} example (\myref{Java_2D_array_month}) slightly:

\begin{lstlisting}[caption=IncorrectMonthException.java]
public class IncorrectMonthException extends Exception
{
	private int index;

	public IncorrectMonthException(int index)
	{
		this.index = index;
	} 
	public int getIndex()
	{
		return index;
	}
}
\end{lstlisting}

\begin{lstlisting}[caption=Month2.java]
class Month2
{
	public static String[] months = 
	{
		"January", 
		"February", 
		"March", 
		"April",
		"May",
		"June",
		"July",
		"August",
		"September",
		"October",
		"November",
		"December"
	};

	public static String get_month (int i) throws IncorrectMonthException
	{
		if (i<0 || i>11)
			throw new IncorrectMonthException(i);
		return months[i];
	};

	public static void main (String[] args)
	{
		try
		{
			System.out.println(get_month(100));
		}
		catch(IncorrectMonthException e)
		{
			System.out.println("incorrect month index: "+ e.getIndex());
			e.printStackTrace();
		}
	};
}
\end{lstlisting}

Essentially, \TT{IncorrectMonthException.class} has just object constructor and one accessor method.

\TT{IncorrectMonthException} class is inherited from \TT{Exception}, 
so \TT{IncorrectMonthException} constructor
first calls constructor of \TT{Exception} class, 
then it put incoming integer value into the sole \TT{IncorrectMonthException} class field:

\begin{lstlisting}
  public IncorrectMonthException(int);
    flags: ACC_PUBLIC
    Code:
      stack=2, locals=2, args_size=2
         0: aload_0       
         1: invokespecial #1                  // Method java/lang/Exception."<init>":()V
         4: aload_0       
         5: iload_1       
         6: putfield      #2                  // Field index:I
         9: return        
\end{lstlisting}

\TT{getIndex()} is just accessor. 
Reference to \TT{IncorrectMonthException} is passed in zeroth \ac{LVA} slot
(\IT{this}), \TT{aload\_0} takes it, \TT{getfield} loads integer value, \TT{ireturn} returns it.

\begin{lstlisting}
  public int getIndex();
    flags: ACC_PUBLIC
    Code:
      stack=1, locals=1, args_size=1
         0: aload_0       
         1: getfield      #2                  // Field index:I
         4: ireturn       
\end{lstlisting}

Now let's take a look on \TT{get\_month()} in \TT{Month2.class}:

\begin{lstlisting}[caption=Month2.class]
  public static java.lang.String get_month(int) throws IncorrectMonthException;
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=3, locals=1, args_size=1
         0: iload_0       
         1: iflt          10
         4: iload_0       
         5: bipush        11
         7: if_icmple     19
        10: new           #2                  // class IncorrectMonthException
        13: dup           
        14: iload_0       
        15: invokespecial #3                  // Method IncorrectMonthException."<init>":(I)V
        18: athrow        
        19: getstatic     #4                  // Field months:[Ljava/lang/String;
        22: iload_0       
        23: aaload        
        24: areturn       
\end{lstlisting}

\TT{iflt} at offset 1 is \IT{if less than}.

In case of invalid index, a new object is created using \TT{new} instruction at offset 10.
Object type is passed as argument to the instruction (\TT{IncorrectMonthException}).
Then its constructor is called with index integer passed via \ac{TOS} (offset 15).
At offset 18 object is constructed, now \TT{athrow} instruction takes reference to newly constructed
object and signalling to JVM to find appropriate exception handler.
Nothing happened after \TT{athrow} instruction here, so at offset 19 is another basic block,
not related to exceptions business, we can get here from offset 7.

How handler works? Now take a look at \TT{main()} in \TT{Month2.class}:

\begin{lstlisting}[caption=Month2.class]
  public static void main(java.lang.String[]);
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=3, locals=2, args_size=1
         0: getstatic     #5                  // Field java/lang/System.out:Ljava/io/PrintStream;
         3: bipush        100
         5: invokestatic  #6                  // Method get_month:(I)Ljava/lang/String;
         8: invokevirtual #7                  // Method java/io/PrintStream.println:(Ljava/lang/String;)V
        11: goto          47
        14: astore_1      
        15: getstatic     #5                  // Field java/lang/System.out:Ljava/io/PrintStream;
        18: new           #8                  // class java/lang/StringBuilder
        21: dup           
        22: invokespecial #9                  // Method java/lang/StringBuilder."<init>":()V
        25: ldc           #10                 // String incorrect month index: 
        27: invokevirtual #11                 // Method java/lang/StringBuilder.append:(Ljava/lang/String;)Ljava/lang/StringBuilder;
        30: aload_1       
        31: invokevirtual #12                 // Method IncorrectMonthException.getIndex:()I
        34: invokevirtual #13                 // Method java/lang/StringBuilder.append:(I)Ljava/lang/StringBuilder;
        37: invokevirtual #14                 // Method java/lang/StringBuilder.toString:()Ljava/lang/String;
        40: invokevirtual #7                  // Method java/io/PrintStream.println:(Ljava/lang/String;)V
        43: aload_1       
        44: invokevirtual #15                 // Method IncorrectMonthException.printStackTrace:()V
        47: return        
      Exception table:
         from    to  target type
             0    11    14   Class IncorrectMonthException
\end{lstlisting}

Here is \TT{Exception table}, which defines that from offset 0 to 11 (inclusive) an exception 
\TT{IncorrectMonthException} may happen, and if it's so, pass control flow to offset 14.
Indeed, the main program is ended at offset 11.
At the offset 14 handler started, it's not possible to get here, there are no conditional/unconditional
jumps to this area.
But JVM will transfer execution flow here in case of exception.
The very first \TT{astore\_1} (at 14) takes incoming reference to exception object and stores it to
\ac{LVA} slot 1.
Later, \TT{getIndex()} method will be called for it at offset 31. 
Reference to current exception object is passed right before (offset 30).
All the rest of the code is string manipulating code: first integer value returned by \TT{getIndex()}
is converted into string by \TT{toString()} method, then it concatenates with 
``incorrect month index: '' text string (like we already considered before), 
then \TT{println()} and \TT{printStackTrace()} are called.
After \TT{printStackTrace()} finishes, exception is handled, we can proceed to normal functioning.
At offset 47 there are \TT{return} which finishes \TT{main()} function, but there could be any other
code which executes as if no exceptions raised.

Here is an example, how IDA shows exception ranges:

\begin{lstlisting}
    .catch java/io/FileNotFoundException from met001_335 to met001_360\
 using met001_360
    .catch java/io/FileNotFoundException from met001_185 to met001_214\
 using met001_214
    .catch java/io/FileNotFoundException from met001_181 to met001_192\
 using met001_195
    .catch java/io/FileNotFoundException from met001_155 to met001_176\
 using met001_176
    .catch java/io/FileNotFoundException from met001_83 to met001_129 using \
met001_129
    .catch java/io/FileNotFoundException from met001_42 to met001_66 using \
met001_69
    .catch java/io/FileNotFoundException from met001_begin to met001_37\
 using met001_37
\end{lstlisting}
