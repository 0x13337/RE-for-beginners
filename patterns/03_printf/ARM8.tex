\subsection{ARM: \IFRU{8 аргументов}{8 arguments}}

\IFRU{Снова воспользуемся примером с 9-ю аргументами из предыдущей секции}{Let's use again the example
with 9 arguments from the previous section}: \ref{example_printf8_x64}.

\begin{lstlisting}
void printf_main2()
{
	printf("a=%d; b=%d; c=%d; d=%d; e=%d; f=%d; g=%d; h=%d\n", 1, 2, 3, 4, 5, 6, 7, 8);
};
\end{lstlisting}

\subsubsection{\OptimizingKeil: \ARMMode}

\begin{lstlisting}
.text:00000028             printf_main2
.text:00000028
.text:00000028             var_18          = -0x18
.text:00000028             var_14          = -0x14
.text:00000028             var_4           = -4
.text:00000028
.text:00000028 04 E0 2D E5                 STR     LR, [SP,#var_4]!
.text:0000002C 14 D0 4D E2                 SUB     SP, SP, #0x14
.text:00000030 08 30 A0 E3                 MOV     R3, #8
.text:00000034 07 20 A0 E3                 MOV     R2, #7
.text:00000038 06 10 A0 E3                 MOV     R1, #6
.text:0000003C 05 00 A0 E3                 MOV     R0, #5
.text:00000040 04 C0 8D E2                 ADD     R12, SP, #0x18+var_14
.text:00000044 0F 00 8C E8                 STMIA   R12, {R0-R3}
.text:00000048 04 00 A0 E3                 MOV     R0, #4
.text:0000004C 00 00 8D E5                 STR     R0, [SP,#0x18+var_18]
.text:00000050 03 30 A0 E3                 MOV     R3, #3
.text:00000054 02 20 A0 E3                 MOV     R2, #2
.text:00000058 01 10 A0 E3                 MOV     R1, #1
.text:0000005C 6E 0F 8F E2                 ADR     R0, aADBDCDDDEDFDGD ; "a=%d; b=%d; c=%d; d=%d; e=%d; f=%d; g=%"...
.text:00000060 BC 18 00 EB                 BL      __2printf
.text:00000064 14 D0 8D E2                 ADD     SP, SP, #0x14
.text:00000068 04 F0 9D E4                 LDR     PC, [SP+4+var_4],#4
\end{lstlisting}

\IFRU{Этот код можно условно разделить на несколько частей}{This code can be divided into several parts}:

\begin{itemize}
\index{Function prologue}
\item \IFRU{Пролог функции}{Function prologue}:

\index{ARM!\Instructions!STR}
\IFRU{Самая первая инструкция}{The very first} \TT{``STR LR, [SP,\#var\_4]!''} 
\IFRU{сохраняет в стеке \LR, ведь нам придется использовать этот регистр для вызова \printf}
{instruction saves the \LR on the stack, because we will use this register for \printf call}.

\index{ARM!\Instructions!SUB}
\IFRU{Вторая инструкция}{The second} \TT{``SUB SP, SP, \#0x14''}
\IFRU{уменьшает \glslink{stack pointer}{указатель стека} \SP, но, на самом деле, эта процедура нужна для выделения в локальном стеке места размером \TT{0x14} ($20$) байт}
{instruction decreasing
\SP \gls{stack pointer}, but in fact, 
this procedure is needed for allocating a space of size \TT{0x14} ($20$) bytes in the stack}.
\IFRU{Действительно, нам нужно передать 5 32-битных значений через стек в \printf, каждое значение занимает 4 байта, а $5*4=20$ ~--- как раз}
{Indeed, we need to pass 5 32-bit values via stack to the \printf function, and each one occupy 4 bytes, that is $5*4=20$~---exactly}.
\IFRU{Остальные 4 32-битных значения будут переданы через регистры}{Other 4 32-bit values will be passed in
registers}.

\item \IFRU{Передача 5, 6, 7 и 8 через стек}{Passing 5, 6, 7 and 8 via stack}:

\IFRU{Затем значения 5, 6, 7 и 8 записываются в регистры \Reg{0}, \Reg{1}, \Reg{2} и \Reg{3} соответственно}
{Then values 5, 6, 7 and 8
are written to the \Reg{0}, \Reg{1}, \Reg{2} and \Reg{3} registers respectively}.
\IFRU{Затем инструкция}{Then} \TT{``ADD R12, SP, \#0x18+var\_14''} 
\IFRU{записывает в регистр \TT{R12} адрес места в стеке, куда будут помещены эти 4 значения}
{instruction writes an address of the point in the stack, where these 4 variables will be written, into the \TT{R12} register}.
\index{IDA!var\_?}
\IT{var\_14} \IFRU{~--- это макрос ассемблера}{is an assembly macro}, \IFRU{равный}{equal to} $-0x14$, 
\IFRU{такие макросы создает \IDA, чтобы удобнее было показывать, как код обращается к стеку}
{such macros are created by \IDA in order to demonstrate simply how code accessing stack}.
\IFRU{Макросы \IT{var\_?}, создаваемые \IDA, отражают локальные переменные в стеке}{\IT{var\_?} macros created
by \IDA reflecting local variables in the stack}.
\IFRU{Так что в \TT{R12} будет записано $SP+4$}{So, $SP+4$ will be written into the \TT{R12} register}.
\index{ARM!\Instructions!STMIA}
\IFRU{Следующая инструкция}{The next} \TT{``STMIA R12, {R0-R3}''} 
\IFRU{записывает содержимое регистров \Reg{0}-\Reg{3} по адресу в памяти, на который указывает \TT{R12}}
{instruction
writes \Reg{0}-\Reg{3} registers contents at the point in memory to which \TT{R12} pointing}.
\IFRU{Инструкция }\TT{STMIA} \IFRU{означает}{instruction meaning} \IT{Store Multiple Increment After}. 
\IT{Increment After} \IFRU{означает, что \TT{R12} будет увеличиваться на 4 после записи каждого значения регистра}
{meaning the \TT{R12} will be increasing by $4$ after each register value write}.

\item \IFRU{Передача $4$ через стек}{Passing $4$ via stack}:
\IFRU{$4$ записывается в \Reg{0}, затем это значение при помощи инструкции}{$4$ is stored in the \Reg{0} and then,
this value, with the help of} \TT{``STR R0, [SP,\#0x18+var\_18]''} \IFRU{попадает в стек}{instruction, is saved
on the stack}.
\IT{var\_18} \IFRU{равен}{is} $-0x18$, \IFRU{смещение будет $0$}{offset will be $0$}, 
\IFRU{так что, значение из регистра \Reg{0} ($4$) запишется туда, куда указывает \SP}
{so, value from the \Reg{0} register ($4$) will be written to a point the \SP pointing to}.

\item \IFRU{Передача 1, 2 и 3 через регистры}{Passing 1, 2 and 3 via registers}:

\IFRU{Значения для первых трех чисел (a, b, c) (1, 2, 3 соответственно) передаются в регистрах R1, R2 и R3 перед самим вызовом \printf}
{Values of first 3 numbers (a, b, c) (1, 2, 3 respectively) are passing in R1, R2 and R3
registers right before \printf call}, \IFRU{а остальные 5 значений передаются через стек, и вот как}{and other
5 values are passed via stack and this is how}:

\item \IFRU{Вызов \printf}{\printf call}:

\index{Function epilogue}
\item \IFRU{Эпилог функции}{Function epilogue}:

\RU{Инструкция }\TT{``ADD SP, SP, \#0x14''} \IFRU{возвращает \SP на прежнее место, 
аннулируя таким образом всё, что было записано в стеке}
{instruction returns the \SP pointer back to former point,
thus annulling what was written to stack}.
\IFRU{Конечно, то что было записано в стек, там пока и останется, но всё это будет многократно 
перезаписано во время исполнения последующих функций}
{Of course, what was written on the stack will stay there, but it all will be
rewritten while execution of following functions}.

\index{ARM!\Instructions!LDR}
\RU{Инструкция }\TT{``LDR PC, [SP+4+var\_4],\#4''} \IFRU{загружает в \PC сохраненное значение \LR из стека,
обеспечивая таким образом выход из функции}
{instruction loads saved \LR value from the stack into the \PC register, providing
exit from the function}.

\end{itemize}

\subsubsection{\OptimizingKeil: \ThumbMode}

\begin{lstlisting}
.text:0000001C             printf_main2
.text:0000001C
.text:0000001C             var_18          = -0x18
.text:0000001C             var_14          = -0x14
.text:0000001C             var_8           = -8
.text:0000001C
.text:0000001C 00 B5                       PUSH    {LR}
.text:0000001E 08 23                       MOVS    R3, #8
.text:00000020 85 B0                       SUB     SP, SP, #0x14
.text:00000022 04 93                       STR     R3, [SP,#0x18+var_8]
.text:00000024 07 22                       MOVS    R2, #7
.text:00000026 06 21                       MOVS    R1, #6
.text:00000028 05 20                       MOVS    R0, #5
.text:0000002A 01 AB                       ADD     R3, SP, #0x18+var_14
.text:0000002C 07 C3                       STMIA   R3!, {R0-R2}
.text:0000002E 04 20                       MOVS    R0, #4
.text:00000030 00 90                       STR     R0, [SP,#0x18+var_18]
.text:00000032 03 23                       MOVS    R3, #3
.text:00000034 02 22                       MOVS    R2, #2
.text:00000036 01 21                       MOVS    R1, #1
.text:00000038 A0 A0                       ADR     R0, aADBDCDDDEDFDGD ; "a=%d; b=%d; c=%d; d=%d; e=%d; f=%d; g=%"...
.text:0000003A 06 F0 D9 F8                 BL      __2printf
.text:0000003E
.text:0000003E             loc_3E                                  ; CODE XREF: example13_f+16
.text:0000003E 05 B0                       ADD     SP, SP, #0x14
.text:00000040 00 BD                       POP     {PC}
\end{lstlisting}

\IFRU{Это почти то же самое, что и в предыдущем примере, только код для thumb и значения помещаются в 
стек немного иначе: в начале $8$ за первый раз, затем $5$, $6$, $7$ за второй раз и $4$ за третий раз}{Almost 
same as
in previous example, however, this is thumb code and values are packed into stack differently: 
$8$ for the first time, then $5$, $6$, $7$ for the second and $4$ for the third}.

\subsubsection{\OptimizingXcode: \ARMMode}

\begin{lstlisting}
__text:0000290C             _printf_main2
__text:0000290C
__text:0000290C             var_1C          = -0x1C
__text:0000290C             var_C           = -0xC
__text:0000290C
__text:0000290C 80 40 2D E9                 STMFD           SP!, {R7,LR}
__text:00002910 0D 70 A0 E1                 MOV             R7, SP
__text:00002914 14 D0 4D E2                 SUB             SP, SP, #0x14
__text:00002918 70 05 01 E3                 MOV             R0, #0x1570
__text:0000291C 07 C0 A0 E3                 MOV             R12, #7
__text:00002920 00 00 40 E3                 MOVT            R0, #0
__text:00002924 04 20 A0 E3                 MOV             R2, #4
__text:00002928 00 00 8F E0                 ADD             R0, PC, R0
__text:0000292C 06 30 A0 E3                 MOV             R3, #6
__text:00002930 05 10 A0 E3                 MOV             R1, #5
__text:00002934 00 20 8D E5                 STR             R2, [SP,#0x1C+var_1C]
__text:00002938 0A 10 8D E9                 STMFA           SP, {R1,R3,R12}
__text:0000293C 08 90 A0 E3                 MOV             R9, #8
__text:00002940 01 10 A0 E3                 MOV             R1, #1
__text:00002944 02 20 A0 E3                 MOV             R2, #2
__text:00002948 03 30 A0 E3                 MOV             R3, #3
__text:0000294C 10 90 8D E5                 STR             R9, [SP,#0x1C+var_C]
__text:00002950 A4 05 00 EB                 BL              _printf
__text:00002954 07 D0 A0 E1                 MOV             SP, R7
__text:00002958 80 80 BD E8                 LDMFD           SP!, {R7,PC}
\end{lstlisting}

\index{ARM!\Instructions!STMFA}
\index{ARM!\Instructions!STMIB}
\IFRU{Почти то же самое, что мы уже видели, за исключением того, что}
{Almost the same what we already figured out, with the
exception of} \TT{STMFA} (Store Multiple Full Ascending) 
\IFRU{~--- это синоним инструкции}{instruction, it is synonym to} 
\TT{STMIB} (Store Multiple Increment Before) \EN{instruction}. 
\IFRU{Эта инструкция увеличивает \SP и только затем записывает в память значение очередного регистра, 
но не наоборот}{This
instruction increasing value in the \SP register and only then writing next register value into memory, but not vice versa}.

\IFRU{Второе, что бросается в глаза, это то, что инструкции как будто бы расположены случайно}{Another thing
we easily spot is the instructions are ostensibly located randomly}.
\IFRU{Например, значение в регистре \Reg{0} подготавливается в трех местах, по адресам \TT{0x2918}, \TT{0x2920} 
и \TT{0x2928}, 
когда это можно было бы сделать в одном месте}{For instance, value in the \Reg{0} register is prepared in three
places, at addresses \TT{0x2918}, \TT{0x2920} and \TT{0x2928}, when it would be possible to do it in one single point}.
\IFRU{Однако, у оптимизирующего компилятора могут быть свои доводы о том, как лучше составлять инструкции 
друг с другом для лучшей эффективности исполнения}
{However, optimizing compiler has its own reasons about how
to place instructions better}.
\IFRU{Процессор обычно пытается исполнять одновременно идущие друг за другом инструкции}
{Usually, processor attempts to simultaneously execute instructions located side-by-side}.
\IFRU{К примеру, инструкции}{For example, instructions like} \TT{``MOVT R0, \#0''} \AndENRU 
\TT{``ADD R0, PC, R0''} \IFRU{не могут быть исполнены одновременно, потому что обе инструкции модифицируют 
регистр \Reg{0}}{cannot be executed simultaneously since they both modifying the \Reg{0} register}. 
\IFRU{А вот инструкции}{On the other hand,} \TT{``MOVT R0, \#0''} \AndENRU \TT{``MOV R2, \#4''} 
\IFRU{легко можно исполнить одновременно, 
потому что эффекты от их исполнения никак не конфликтуют друг с другом}{instructions can be executed
simultaneously since effects of their execution are not conflicting with each other}.
\IFRU{Вероятно, компилятор старается генерировать код именно таким образом там, где это возможно}
{Presumably, compiler tries to generate code in such a way, where it is possible, of course}.
 
\subsubsection{\OptimizingXcode: \ThumbTwoMode}

\begin{lstlisting}
__text:00002BA0                   _printf_main2
__text:00002BA0
__text:00002BA0                   var_1C          = -0x1C
__text:00002BA0                   var_18          = -0x18
__text:00002BA0                   var_C           = -0xC
__text:00002BA0
__text:00002BA0 80 B5                             PUSH            {R7,LR}
__text:00002BA2 6F 46                             MOV             R7, SP
__text:00002BA4 85 B0                             SUB             SP, SP, #0x14
__text:00002BA6 41 F2 D8 20                       MOVW            R0, #0x12D8
__text:00002BAA 4F F0 07 0C                       MOV.W           R12, #7
__text:00002BAE C0 F2 00 00                       MOVT.W          R0, #0
__text:00002BB2 04 22                             MOVS            R2, #4
__text:00002BB4 78 44                             ADD             R0, PC  ; char *
__text:00002BB6 06 23                             MOVS            R3, #6
__text:00002BB8 05 21                             MOVS            R1, #5
__text:00002BBA 0D F1 04 0E                       ADD.W           LR, SP, #0x1C+var_18
__text:00002BBE 00 92                             STR             R2, [SP,#0x1C+var_1C]
__text:00002BC0 4F F0 08 09                       MOV.W           R9, #8
__text:00002BC4 8E E8 0A 10                       STMIA.W         LR, {R1,R3,R12}
__text:00002BC8 01 21                             MOVS            R1, #1
__text:00002BCA 02 22                             MOVS            R2, #2
__text:00002BCC 03 23                             MOVS            R3, #3
__text:00002BCE CD F8 10 90                       STR.W           R9, [SP,#0x1C+var_C]
__text:00002BD2 01 F0 0A EA                       BLX             _printf
__text:00002BD6 05 B0                             ADD             SP, SP, #0x14
__text:00002BD8 80 BD                             POP             {R7,PC}
\end{lstlisting}

\IFRU{Почти то же самое, что и в предыдущем примере,
лишь за тем исключением, что здесь используются thumb-инструкции}
{Almost the same as in previous example,
with the exception the thumb-instructions are used here instead}.

