\subsection{ARM64}

\subsubsection{\Optimizing GCC (Linaro) 4.9}

\index{Fused multiply–add}
\index{ARM!\Instructions!MADD}
\RU{Тут всё просто}\EN{That's easy}.
\EN{\TT{MADD} is just an instruction doing fused multiply/add (similar to \TT{MLA} we already saw).}
\RU{\TT{MADD} это просто инструкция производящая умножение/сложение одновременно (как \TT{MLA}, 
которую мы уже видели).}
\EN{All 3 arguments are passed in 32-bit part of X-registers.}
\RU{Все 3 аргумента передаются в 32-битных частях X-регистров.}
\EN{Indeed, argument types are 32-bit \IT{int}'s.}
\RU{Действительно, типы аргументов это 32-битные \IT{int}'ы.}
\EN{The result is returned in \TT{W0}.}
\RU{Результат возвращается в \TT{W0}.}

\begin{lstlisting}
f:
	madd	w0, w0, w1, w2
	ret

main:
	stp	x29, x30, [sp, -16]!
	mov	w2, 3
	mov	w1, 2
	add	x29, sp, 0
	mov	w0, 1
	bl	f
	mov	w1, w0
	adrp	x0, .LC7
	add	x0, x0, :lo12:.LC7
	bl	printf
	mov	w0, 0
	ldp	x29, x30, [sp], 16
	ret

.LC7:
	.string	"%d\n"
\end{lstlisting}

\EN{I also extended all data types to 64-bit \TT{uint64\_t} and tried it:}
\RU{Я также расширил все типы данных до 64-битных \TT{uint64\_t} и попробовал:}

\begin{lstlisting}
uint64_t f (uint64_t a, uint64_t b, uint64_t c)
{
	return a*b+c;
};

int main() 
{
	printf ("%lld\n", f9(0x1122334455667788, 
				0x1111111122222222, 
				0x3333333344444444));
	return 0;
};
\end{lstlisting}

\begin{lstlisting}
f:
	madd	x0, x0, x1, x2
	ret
main:
	mov	x1, 13396
	adrp	x0, .LC8
	stp	x29, x30, [sp, -16]!
	movk	x1, 0x27d0, lsl 16
	add	x0, x0, :lo12:.LC8
	movk	x1, 0x122, lsl 32
	add	x29, sp, 0
	movk	x1, 0x58be, lsl 48
	bl	printf
	mov	w0, 0
	ldp	x29, x30, [sp], 16
	ret

.LC8:
	.string	"%lld\n"
\end{lstlisting}

\EN{Function f() is just the same, but whole 64-bit X-registers are now used.}
\RU{Ф-ция f() точно такая же, только теперь используются полные части 64-битных X-регистров.}
\RU{Длинные 64-битные значения загружаются в регистры по частям, я описал это здесь}{Long 64-bit values 
are loaded into registers by parts, I described it also here}: \ref{ARM_big_constants_loading}.

\subsubsection{\NonOptimizing GCC (Linaro) 4.9}

\EN{Non-optimizing compiler more redundant:}
\RU{Неоптимизирующий компилятор выдает немного лишнего кода:}

\begin{lstlisting}
f:
	sub	sp, sp, #16
	str	w0, [sp,12]
	str	w1, [sp,8]
	str	w2, [sp,4]
	ldr	w1, [sp,12]
	ldr	w0, [sp,8]
	mul	w1, w1, w0
	ldr	w0, [sp,4]
	add	w0, w1, w0
	add	sp, sp, 16
	ret
\end{lstlisting}

\EN{The code saves input arguments in the local stack, 
for a case if someone (or something) in this function will need to use \TT{W0...W2} 
registers by overwriting original
function arguments, but they may be needed again in future.}
\RU{Код сохраняет входные аргументы в локальном стеке, на случай если кому-то (или чему-то) в этой ф-ции
понадобится использовать регистры \TT{W0...W2}, перезаписывая оригинальные аргументы ф-ции, которые
могут понадобится в будущем.}
\RU{Это называется}\EN{So this is} \IT{Register Save Area} (\cite{ARM64_PCS}), 
\RU{хотя, вызываемая ф-ция не обязана сохранять их}\EN{however, callee is not obliged to save them}.

\RU{Почему оптимизирующий GCC 4.9 убрал этот, сохраняющий аргументы, код?}
\EN{Why optimizing GCC 4.9 dropped this arguments saving code? }
\EN{Because it did some additional optimizing work and concluded
that function arguments will not be needed in future and \TT{W0...W2} registers will also not be used.}
\RU{Потому что он провел дополнительную работу по оптимизации и сделал вывод, что аргументы ф-ции не понадобятся
в будущем и регистры \TT{W0...W2} также не будут использоваться.}

\index{ARM!\Instructions!MUL}
\index{ARM!\Instructions!ADD}
\RU{Также мы видим пару инструкций \TT{MUL}/\TT{ADD} вместо одной \TT{MADD}.}
\EN{We also see \TT{MUL}/\TT{ADD} instruction pair instead of single \TT{MADD}.}
