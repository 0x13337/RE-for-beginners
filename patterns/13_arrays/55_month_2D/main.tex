% TODO: translate
\ifdefined\RUSSIAN
\else
\section{Pack of strings as two-dimensional array}

Let's revisit function which returns name of month: \lstref{get_month1}.
As you may see, at least one memory load operation is needed to prepare a pointer to the string
consisting of month name.
Will it be possible to get rid of this memory load operation?
In fact yes, if to represent list of strings as a two-dimensional array:

\lstinputlisting{patterns/13_arrays/55_month_2D/month2.c}

Here is what we've got:

\lstinputlisting[caption=\Optimizing MSVC 2013 x64]{patterns/13_arrays/55_month_2D/MSVC2013_x64_Ox.asm}

There are no memory access at all. 
All this function do is calculating point at which the first character of month name is: 
$pointer\_to\_the\_table + month * 10$.
There are also two LEA instructions which are effectively working as several MUL and MOV instructions.

Width of the array is 10 bytes. 
Indeed, longest string here is ``September'' (9 bytes) plus terminating zero is 10 bytes.
Other month names are padded by zero bytes, so they all occupy the same space (10 bytes).
Thus, our function works even faster, because all string are started at points which can be easily
calculated.

GCC 4.9 can do it even shorter:

\begin{lstlisting}[caption=\Optimizing GCC 4.9 x64]
	movsx	rdi, edi
	lea	rax, [rdi+rdi*4]
	lea	rax, month2[rax+rax]
	ret
\end{lstlisting}

LEA is also used for multiplication by 10 here.

Non-optimizing compilers do multiplication differently.

\begin{lstlisting}[caption=\NonOptimizing GCC 4.9 x64]
get_month2:
	push	rbp
	mov	rbp, rsp
	mov	DWORD PTR [rbp-4], edi
	mov	eax, DWORD PTR [rbp-4]
	movsx	rdx, eax
; RDX = sign-extended input value
	mov	rax, rdx
; RAX = month
	sal	rax, 2
; RAX = month<<2 = month*4
	add	rax, rdx
; RAX = RAX+RDX = month*4+month = month*5
	add	rax, rax
; RAX = RAX*2 = month*5*2 = month*10
	add	rax, OFFSET FLAT:month2
; RAX = month*10 + pointer to the table
	pop	rbp
	ret
\end{lstlisting}

\NonOptimizing MSVC just use IMUL instruction:
\index{x86!\Instructions!IMUL}

\lstinputlisting[caption=\NonOptimizing MSVC 2013 x64]{patterns/13_arrays/55_month_2D/MSVC2013_x64.asm}

\index{\CompilerAnomaly}
\label{MSVC2013_anomaly}
\dots but one thing is weird here: why to add multiplication by zero and adding zero to the final result?
I don't know, this looks like compiler code generator quirk, which wasn't catched by compiler's tests
(resulting code is correct after all).
I intentionally add such pieces of code so the reader would understand, 
that sometimes one shouldn't puzzle over such compiler's artifacts.

\ifdefined\IncludeARM
\subsection{32-bit ARM}

Optimizing Keil for Thumb mode use multiplication instruction MULS:

\begin{lstlisting}[caption=\OptimizingKeilVI (\ThumbMode)]
; R0 = month
        MOVS     r1,#0xa
; R1 = 10
        MULS     r0,r1,r0
; R0 = R1*R0 = 10*month
        LDR      r1,|L0.68|
; R1 = pointer to the table
        ADDS     r0,r0,r1
; R0 = R0+R1 = 10*month + pointer to the table
        BX       lr
\end{lstlisting}

Optimizing Keil for ARM mode use add and shift operations:

\begin{lstlisting}[caption=\OptimizingKeilVI (\ARMMode)]
; R0 = month
        LDR      r1,|L0.104|
; R1 = pointer to the table
        ADD      r0,r0,r0,LSL #2
; R0 = R0+R0<<2 = R0+R0*4 = month*5
        ADD      r0,r1,r0,LSL #1
; R0 = R1+R0<<2 = pointer to the table + month*5*2 = pointer to the table + month*10
        BX       lr
\end{lstlisting}

\subsection{ARM64}

\begin{lstlisting}[caption=\Optimizing GCC 4.9 ARM64]
; W0 = month
	sxtw	x0, w0
; X0 = sign-extended input value
	adrp	x1, .LANCHOR1
	add	x1, x1, :lo12:.LANCHOR1
; X1 = pointer to the table
	add	x0, x0, x0, lsl 2
; X0 = X0+X0<<2 = X0+X0*4 = X0*5
	add	x0, x1, x0, lsl 1
; X0 = X1+X0<<1 = X1+X0*2 = pointer to the table + X0*10
	ret
\end{lstlisting}

\index{ARM!\Instructions!SXTW}
\index{ARM!\Instructions!ADRP/ADD pair}
SXTW is used for sign-extension and promoting input 32-bit value into 64-bit one and storing it in X0.
ADRP/ADD pair is used for loading address of the table.
ADD instructions also has LSL suffix, which helps with multiplications.
\fi

\ifdefined\IncludeMIPS
\subsection{MIPS}
\lstinputlisting[caption=\Optimizing GCC 4.4.5 (IDA)]{patterns/13_arrays/55_month_2D/MIPS_O3_IDA.lst}
\fi

\subsection{Conclusion}

This is a bit old-school technique to store strings.
You may find a lot of it in \oracle, for example.
But I don't really know if it's worth to do it on modern computers.
Nevertheless, it was a good example of arrays, so I added it to this book.
\fi
